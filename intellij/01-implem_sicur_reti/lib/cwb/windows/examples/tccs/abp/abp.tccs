**********************************************************************
*  abp.ccs   
*  Author:  Rance Cleaveland
*
*  This file contains a version of the Alternating Bit Protocol.
*
*  The specification of the protocol is represented by the agent
*  "Spec", and there are two implementations, "ABP-lossy", which
*  uses a lossy medium "Mlossy", and "ABP-safe, which uses a
*  reliable medum "Msafe".
*
*  "ABP-lossy" is correct in the sense that it is observationally
*  equivalent to "Spec", whereas "ABP-safe" is not.  "ABP-safe"
*  in fact may deadlock as can be detected with the model checker.
*
**********************************************************************

**********************************************************************
*  The definition of the specification "Spec"
**********************************************************************

proc Spec = send.'receive.Spec 

**********************************************************************
*  The definition of the sender
**********************************************************************

proc S0 = send.S0'

proc S0' = 's0.(rack0.S1 + rack1.S0' + t.S0')

proc S1 = send.S1'

proc S1' = 's1.(rack1.S0 + rack0.S1' + t.S1')

**********************************************************************
*  The definition of the receiver
**********************************************************************

proc R0 = r0.'receive.'sack0.R1 + r1.'sack1.R0 + t.'sack1.R0

proc R1 = r1.'receive.'sack1.R0 + r0.'sack0.R1 + t.'sack0.R1

**********************************************************************
*  The definition of the reliable medium "Msafe"
**********************************************************************

proc Msafe =
    s0.'r0.Msafe + s1.'r1.Msafe + 
    sack0.'rack0.Msafe + sack1.'rack1.Msafe

**********************************************************************
*  The definition of the lossy medium "Mlossy"
**********************************************************************

proc Mlossy =
    s0.('r0.Mlossy + Mlossy) + s1.('r1.Mlossy + Mlossy)   
       + sack0.('rack0.Mlossy + Mlossy) + sack1.('rack1.Mlossy + Mlossy)

**********************************************************************
*  The Internal actions to be hidden
**********************************************************************

set Internals = { r0,r1,s0,s1,rack0,rack1,sack0,sack1 }

**********************************************************************
*  The definition of the safe implementation "ABP-safe"
**********************************************************************

proc ABP-safe = (R0 | Msafe | S0) \ Internals

**********************************************************************
*  The definition of the lossy implementation "ABP-lossy"
**********************************************************************

proc ABP-lossy = (R0 | Mlossy | S0) \ Internals

**********************************************************************
*  Two-link network can be built by chaining ABP links
*  Added by Steve Sims (8-16-95)
**********************************************************************

**********************************************************************
*  Specification
**********************************************************************

proc Spec2 = send.Spec2'

proc Spec2' = send.Spec2'' + 'receive.Spec2

proc Spec2'' = 'receive.Spec2' 

**********************************************************************
*  Implementation
**********************************************************************

proc Two-link-netw =
   (ABP-lossy[inode/receive]|ABP-lossy[inode/send])\{inode}

**********************************************************************
*  Three-link network
**********************************************************************

**********************************************************************
*  Specification
**********************************************************************

proc Spec3 = send.Spec3'

proc Spec3' = send.Spec3'' + 'receive.Spec3

proc Spec3'' = send.Spec3''' + 'receive.Spec3'

proc Spec3''' = 'receive.Spec3''

**********************************************************************
*  Implementation
**********************************************************************

proc Three-link-netw =
   (
     ABP-lossy[inode1/receive] |
     ABP-lossy[inode1/send,inode2/receive] |
     ABP-lossy[inode2/send]      
   )\{inode1,inode2}
